project.ext {
    setAppDefaultConfig = {
        extension ->
            extension.apply plugin: 'com.android.application'
            extension.apply plugin: 'kotlin-kapt'
            extension.apply plugin: 'kotlin-android'
            extension.apply plugin: 'kotlin-android-extensions'
            extension.description "app"
            setAndroidConfig extension.android
    }

    setModuleDefaultConfig = {
        extension ->
            if (isMoudle.toBoolean()) {
                extension.apply plugin: 'com.android.application'
            } else {
                extension.apply plugin: 'com.android.library'
            }
            extension.apply plugin: 'kotlin-kapt'
            extension.apply plugin: 'kotlin-android'
            extension.apply plugin: 'kotlin-android-extensions'
            extension.description "module"
            setAndroidConfig extension.android

            extension.android.sourceSets {
                main {
                    if (isMoudle.toBoolean()) {
                        manifest.srcFile 'src/main/AndroidManifest.xml'
                    } else {
                        manifest.srcFile 'src/main/release/AndroidManifest.xml'
                        java {
                            exclude 'debug/**'
                        }
                    }
                }
            }
    }

    setLibDefaultConfig = {
        extension ->
            extension.apply plugin: 'com.android.library'
            extension.apply plugin: 'kotlin-kapt'
            extension.apply plugin: 'kotlin-android'
            extension.apply plugin: 'kotlin-android-extensions'
            extension.description "lib"
            setAndroidConfig extension.android
    }

    setAndroidConfig = {
        extension ->
            extension.compileSdkVersion rootProject.ext.android.compileSdkVersion
            extension.buildToolsVersion = rootProject.ext.android.buildToolsVersion
            extension.defaultConfig {
                minSdkVersion rootProject.ext.android.minSdkVersion
                targetSdkVersion rootProject.ext.android.targetSdkVersion
                versionCode rootProject.ext.android.versionCode
                versionName rootProject.ext.android.versionName

                testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

                kapt {
                    arguments {
                        arg("AROUTER_MODULE_NAME", project.getName())
                    }
                }
            }

            extension.buildTypes {
                release {
                    minifyEnabled false
                    proguardFiles extension.getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                }
            }

            extension.dataBinding {
                enabled = true
            }

            extension.compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
    }
}